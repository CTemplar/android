apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

repositories {
    maven { url "https://jitpack.io" }
}

ext {
    versionMajor = 1
    versionMinor = 3
    versionBuild = 7
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "mobileapp.ctemplar.com.ctemplarapp"
        minSdkVersion 16
        versionCode 46
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    flavorDimensions "version"
    productFlavors {
        development {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}-${new Date().format('M.d.yyyy')}.dev"
            applicationIdSuffix '.development'
            buildConfigField("String", "DOMAIN", "\"dev.ctemplar.com\"")
            buildConfigField("String", "ORIGIN", "\"https://dev.ctemplar.net\"")
            buildConfigField("String", "BASE_URL", "\"https://devapi.ctemplar.net/\"")
        }
        production {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}"
            applicationIdSuffix ".production"
            buildConfigField("String", "DOMAIN", "\"ctemplar.com\"")
            buildConfigField("String", "ORIGIN", "\"https://mail.ctemplar.com\"")
            buildConfigField("String", "BASE_URL", "\"https://api.ctemplar.com/\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.webkit:webkit:1.3.0'

    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.dagger:dagger:2.27'
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20200713.1'

    implementation ('com.google.firebase:firebase-messaging:20.2.4') {
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    implementation 'org.bouncycastle:bcpg-jdk15on:1.64'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:butterknife:10.2.1'
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'

    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    annotationProcessor 'com.google.auto.value:auto-value:1.5.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    compileOnly 'com.jakewharton.auto.value:auto-value-annotations:1.4'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
