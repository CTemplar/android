apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

ext {
    versionMajor = 1
    versionMinor = 6
    versionBuild = 1
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "mobileapp.ctemplar.com.ctemplarapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 70
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }
    flavorDimensions "version"
    productFlavors {
        development {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}-${new Date().format('M.d.yyyy')}-dev"
            applicationIdSuffix '.development'
            buildConfigField("String", "DOMAIN", "\"dev.ctemplar.com\"")
            buildConfigField("String", "ORIGIN", "\"https://dev.ctemplar.net\"")
            buildConfigField("String", "BASE_URL", "\"https://devapi.ctemplar.net/\"")
            buildConfigField("String", "BASE_SOCKET_URL", "\"wss://devapi.ctemplar.net/\"")
            buildConfigField("String", "BASE_TOR_URL", "\"https://devapi.ctemplar.net/\"")
            buildConfigField("String", "USER_AGENT", "\"CTemplar App v${versionMajor}.${versionMinor}.${versionBuild}-dev\"")
        }
        production {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}"
            applicationIdSuffix ".production"
            buildConfigField("String", "DOMAIN", "\"ctemplar.com\"")
            buildConfigField("String", "ORIGIN", "\"https://mail.ctemplar.com\"")
            buildConfigField("String", "BASE_URL", "\"https://api.ctemplar.com/\"")
            buildConfigField("String", "BASE_SOCKET_URL", "\"wss://api.ctemplar.com/\"")
            buildConfigField("String", "BASE_TOR_URL", "\"http://api.ctemplarpizuduxk3fkwrieizstx33kg5chlvrh37nz73pv5smsvl6ad.onion/\"")
            buildConfigField("String", "USER_AGENT", "\"CTemplar App v${versionName}\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-runtime:2.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.activity:activity:1.4.0'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.webkit:webkit:1.4.0'

    implementation "androidx.work:work-runtime:2.7.1"
    implementation "androidx.work:work-rxjava2:2.7.1"

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    implementation 'info.guardianproject.netcipher:netcipher-webkit:2.0.0-alpha1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.sentry:sentry-android:5.5.2'

    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.dagger:dagger:2.40.5'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20200713.1'

    implementation ('com.google.firebase:firebase-messaging:23.0.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:butterknife:10.2.1'

    implementation 'org.bouncycastle:bcpg-jdk15on:1.66'

    annotationProcessor 'androidx.room:room-compiler:2.4.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.40.5'
    annotationProcessor 'com.google.auto.value:auto-value:1.5.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    compileOnly 'com.jakewharton.auto.value:auto-value-annotations:1.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
